prototype(WebExcess.Accordion:Container) < prototype(Content) {
	closeOthers = ${q(node).property('closeOthers') ? true : false}
	@context.closeOthers = ${this.closeOthers}
	firstOpen = ${q(node).property('firstOpen') ? true : false}
	@context.firstOpen = ${this.firstOpen}

	content = ContentCollection {
		nodePath = 'main'
		attributes {
			class = Neos.Fusion:RawArray {
				accordion = 'accordion'
				backend = 'neos-accordion-backend'
				backend.@if.isInBackend = ${node.context.inBackend}
			}
			data-close-others = ${closeOthers}
			data-open-first = ${firstOpen}
		}
	}
}

prototype(WebExcess.Accordion:Content) < prototype(Content) {
	attributes.class = Neos.Fusion:RawArray {
		item = 'accordion-item'
		active = ${q(node).property('active') ? 'accordion-item-active' : ''}
	}

	titleAttributes = Neos.Fusion:Attributes {
		class = 'accordion-title'
	}
	contentAttributes = Neos.Fusion:Attributes {
		class = 'accordion-content'
	}

	content = ContentCollection {
		nodePath = 'main'
		attributes {
			class = 'accordion-content-inner'
			class.@process.collectionClass >
		}
	}
}

prototype(Page) {
	head.Accordion = Neos.Fusion:Tag {
		tagName = 'link'
		attributes {
			rel = 'stylesheet'
			href = Neos.Fusion:ResourceUri {
				path = 'resource://WebExcess.Accordion/Public/accordion.min.css'
			}
		}
		@if.isEnabled = ${Configuration.setting('WebExcess.Accordion.includeCss')}
	}
	body.javascripts.Accordion = Neos.Fusion:Tag {
		tagName = 'script'
		attributes.src = Neos.Fusion:ResourceUri {
			path = 'resource://WebExcess.Accordion/Public/accordion.min.js'
		}
		@position = 'end'
		@if.isEnabled = ${Configuration.setting('WebExcess.Accordion.includeJavascript')}
	}
}
